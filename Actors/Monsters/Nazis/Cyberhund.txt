ACTOR Cyberhund 22367
{
Health 50
Radius 14
Height 36
Speed 12
Scale 1.1
PainChance 150
PainChance "SS", 0
MONSTER
+FLOORCLIP
+DONTHURTSPECIES
+FASTER
+FASTMELEE
+LONGMELEERANGE
-CANUSEWALLS
Species "SS"
Damagetype "SS"
Damagefactor "SS", 0.0
MeleeDamage 12
MaxTargetRange 200
SeeSound "Cyberhund/sight"
DeathSound "Cyberhund/death"
PainSound "Cyberhund/Pain"
MeleeSound "Null"
DropItem "LifeEssence" 128
DropItem "ArmorBonusMax" 128
Obituary "%o's skin was chewed off by a Cyberhund."
Tag "$NECybHund"
States
{
Spawn:
	TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ModifyMonster",0,1)
	Goto Idle
  Idle:
		TNT1 A 0 A_Jump(128,"Wander")
		Goto Sitting
Sitting:
		DG03 A 10 A_Look
        Loop
Wander:
		TNT1 A 0 A_Look
    	DG03 BBBB 2 A_Wander
		TNT1 A 0 A_Look
		DG03 CCCC 2 A_Wander
		TNT1 A 0 A_Look
		DG03 DDDD 2 A_Wander
		TNT1 A 0 A_Look
		DG03 EEEE 2 A_Wander
		Loop
See:
    	DG03 BBBBCCCCDDDDEEEE 1 A_Chase
	    Loop
Melee:
		TNT1 A 0 A_PlaySound("Cyberhund/bite")
        DG03 FG 4 A_FaceTarget
		DG03 H 5 A_MeleeAttack
		DG03 GF 5 A_FaceTarget
        Goto See
Missile:
	   TNT1 A 0 A_JumpIfCloser(200,1)
	   Goto See
	   TNT1 A 0 A_PlaySound("Cyberhund/bite")
	   DG03 FG 6 A_FaceTarget
	   TNT1 A 0 A_ChangeFlag(SLIDESONWALLS,1)
	   TNT1 A 0 ThrustThingZ(0,20,0,1)
	   DG03 G 2 A_Recoil(-22)
	   DG03 H 3 A_CustomMissile("CyberhundLunge",18,0,0,CMF_AIMDIRECTION)
	   DG03 H 6
	   DG03 G 2 A_ScaleVelocity(0.2)
	   TNT1 A 0 A_ChangeFlag(SLIDESONWALLS,0)
	   DG03 F 1 A_ScaleVelocity(0)
	   Goto See
Pain: 
	   TNT1 A 0 A_ChangeFlag(SLIDESONWALLS,0)
       DG03 I 4 A_Pain
       Goto See
Death:
	TNT1 A 0 A_ChangeFlag(SLIDESONWALLS,0)
        DG03 J 5 A_Scream
        DG03 K 5 A_Fall
        DG03 L 5 A_SpawnItem("BodyCrash",0,0,0,0)
    	DG03 M -1
        Stop
XDeath:
		TNT1 A 0 A_ChangeFlag(SLIDESONWALLS,0)
        DG03 N 3 A_SpawnItemEx("GibbedGenerator",0,0,0,0,0,0,0,128,0)
        DG03 O 3 A_XScream
        DG03 P 3 A_Fall
        DG03 Q -1
    	Stop 
Raise:
	    TNT1 A 0
	    TNT1 A 0 A_SpawnItemEx("VileHealEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
        DG03 MLKJ 5
        Goto See
    	}
}

ACTOR CyberhundLunge
{
   Height 16
   Radius 16
   Speed 20
   Damage (13*Random(4,8))
   Species "SS"
   DamageType "SS"
   SeeSound ""  
   DeathSound ""
   PROJECTILE
   States
   {
   Spawn:
	TNT1 A 4
    Stop
    }
}