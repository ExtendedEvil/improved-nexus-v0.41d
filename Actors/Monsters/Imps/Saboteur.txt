Actor Saboteur : Imp 26432
{
	Health 210
	Radius 20
	Height 56
	Mass 500
	Speed 10
	PainChance 50
	Scale 1.1
	Monster
	+MISSILEMORE
	+MISSILEEVENMORE
    +QUICKTORETALIATE
	DropItem "LifeEssence" 200
    DropItem "ArmorBonusMax" 200
    DropItem "CFMana" 100 30
	DropItem "CFMana" 50 30
	SeeSound "AdvancedImp/Sight"
	PainSound "AdvancedImp/Pain"
	DeathSound "AdvancedImp/Death"
	ActiveSound "AdvancedImp/Active"
	HitObituary "%o got too close to a Saboteur."
	Obituary "%o was unable to evade a Saboteur's seeker."
	Tag "$NESabot"
	var int user_lowlag;
	States
	{
  Spawn:
	TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ModifyMonster",0,1)
	Goto Idle
  Idle:
	SAIM A 8 A_Look
	Loop
  See:
	TNT1 A 0 A_JumpIf(user_lowlag == 1,"MoreSee")
	TNT1 A 0 A_JumpIfTargetInLOS("MoreSee",0,0,0,0)
	SAIM ABCD 6 A_Chase
	Loop
  MoreSee:
	TNT1 A 0 A_SetUserVar("user_lowlag",1)
  	TNT1 A 0 A_Jump(46,"Evade")
    SAIM AABB 3 A_Chase
	TNT1 A 0 A_SpawnItem("HumanishStep",0,0,0,0)
	SAIM CCDD 3 A_Chase
	TNT1 A 0 A_SpawnItem("HumanishStep",0,0,0,0)
    Loop
  Evade:
	TNT1 A 0 A_Jump(128,7)
	TNT1 A 0 ThrustThing(Angle*256/360+64,16,0,0)
	SAIM AAAA 3 A_Chase
	TNT1 A 0 A_Stop
	Goto See
	TNT1 A 0 ThrustThing(Angle*256/360+192,16,0,0)
	SAIM AAAA 3 A_Chase
	TNT1 A 0 A_Stop
	TNT1 A 0 A_JumpIfTargetInLOS("Missile")
	Goto See
  Melee:
	TNT1 A 0 A_Jump(128,7)
    SAIM EF 4 A_FaceTarget
    SAIM G 3 A_CustomMeleeAttack(35,"imps/melee")
	SAIM FE 4 A_FaceTarget
    Goto See
	SAIM VW 4 A_FaceTarget
    SAIM X 3 A_CustomMeleeAttack(35,"imps/melee")
	SAIM WV 4 A_FaceTarget
	Goto See
  Missile:
    SAIM EF 6 A_FaceTarget
    SAIM G 5 Bright A_CustomMissile("DevilBall", 32, 0, 0)
	SAIM FE 4 A_FaceTarget
	SAIM VW 6 A_FaceTarget
    SAIM X 5 Bright A_CustomMissile("DevilBall", 32, 0, 0)
	SAIM WV 4 A_FaceTarget
	TNT1 A 0 A_Jump(48,"Pause")
	TNT1 A 0 A_SpidRefire
	Goto Missile
  Pause:
    SAIM V 2 
    Goto See
  Pain:
    SAIM H 2 
    SAIM H 2 A_Pain
    goto See
  Death:
    SAIM I 8 A_Scream
    SAIM J 8
    SAIM K 6
	SAIM L 6 A_Fall
	SAIM M -1
    Stop
  XDeath:
	SAIM N 3
	SAIM O 3 A_XScream
	SAIM P 3 A_SpawnItemEx("GibbedGenerator",0,0,0,0,0,0,0,128,0)
	SAIM Q 3 A_Fall
	SAIM RST 3
	SAIM U -1
    Stop
  Raise:
	TNT1 A 0
	TNT1 A 0 A_SpawnItemEx("VileHealEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
	SAIM MLKJ 6
	Goto See
	}
}